#ifndef common_scattering
#define common_scattering

#include lighting.jsl
#include raytracing.jsl

FRAG_IN vec3 scale;

uniform float atmoDensity;
uniform vec3 atmoTint;
uniform vec3 wavelength;

const float kAtmoDensityMult = 50.0;
const float kAtmoScale = 0.025;
const float kPlanetRadius = 50000.0;
const float kOuterRadius = 1.025;

//vec3 kRayleigh = vec3(0.0025) / pow(vec3(0.65, 0.57, 0.475), vec3(4.0));
//vec3 kRayleigh = vec3(0.0025) / pow(vec3(0.45, 0.57, 0.65), vec3(4.0));
//vec3 kRayleigh = vec3(0.0025) / pow(vec3(0.66, 0.53, 0.40), vec3(4.0));
vec3 kRayleigh = 0.4 * vec3(0.0025, 0.0045, 0.0020);
vec3 kMie = vec3(0.002);

const float kDepth = 0.125;
const float kRcpSamples = 1.0 / 8.0;

float phase(float x) {
  return exp(-0.00287 + x * (0.459 + x * (3.83 + x * (-6.80 + 6.25 * x))));
}

float hgPhase(float d, float g) {
  return
    1.5 * (1. - g*g) / (2.0 + g*g) * (1.0 + d*d) /
    pow(1.0 + g*g - 2.0*g*d, 1.5);
}

vec4 shadeAtmosphere(
  vec3 rd,
  vec3 near,
  vec3 far,
  vec3 starDir,
  float occlusion)
{
  vec3 color = vec3(0.0);
  for (float t = 0.5 * kRcpSamples; t < 1.0; t += kRcpSamples) {
    vec3 p = mix(near, far, t);
    float density = exp(-((length(p) - 1.0) / kAtmoScale) / kDepth);
    float inScatter = density * phase(1.0 - dot(starDir, normalize(p)));
    color += density * exp(-radians(720.0) * kDepth * inScatter * (wavelength * kRayleigh + kMie));
  }

  color *= kAtmoDensityMult * atmoDensity * kRcpSamples * length(far - near) / kAtmoScale;
  color *= atmoTint * starColor;
  float d = dot(rd, starDir);
  float rayPhase = 1.0 - occlusion;
  float miePhase = (1.0 - sqrt(occlusion)) * hgPhase(d, 0.75);
  color *= rayPhase * wavelength * kRayleigh + miePhase * kMie;
  return toLinear(vec4(color, saturate(lum(color))));
}

vec4 getScattering(vec3 center, vec3 ro, vec3 rd, float depth) {
  vec3 p = ro + rd * depth;
  float rInner = scale.x;
  float rOuter = rInner * kOuterRadius;
  vec2 inner = interSphere(vec4(center, rInner), ro, rd);
  vec2 outer = interSphere(vec4(center, rOuter), ro, rd);
  if (inner.x < 0 && inner.y > 0)
    return vec4(0.0);
  outer.x = max(outer.x, 0.0);
  if (inner.x > 0)
    outer.y = min(outer.y, inner.x);
  vec3 tn = ((ro + rd * outer.x) - center) / rInner;
  vec3 tf = ((ro + rd * outer.y) - center) / rInner;
  return shadeAtmosphere(rd, tn, tf, normalize(starPos - p), 0.0);
}

vec4 getScatteringInside(vec3 ro, vec3 rd, float depth, float occlusion) {
  vec2 innerT = interSphere(vec4(0.0, 0.0, 0.0, kPlanetRadius), ro, rd);
  vec2 outerT = interSphere(vec4(0.0, 0.0, 0.0, kPlanetRadius * kOuterRadius), ro, rd);

  vec3 near;
  vec3 far;

  /* Space. */
  if (outerT.x > 0.0 && outerT.x < farPlane) {
    near = (ro + rd * outerT.x) / kPlanetRadius;
    far = (ro + rd * min(outerT.y, min(innerT.x, depth))) / kPlanetRadius;
  } else if (outerT.x < 0 && outerT.y > 0) {
    near = ro / kPlanetRadius;
    far = (ro + rd * min(outerT.y, depth)) / kPlanetRadius;
  } else {
    return vec4(0.0);
  }

  return shadeAtmosphere(rd, near, far, normalize(starPos - ro), occlusion);
}

#endif
