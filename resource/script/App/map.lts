type MapWidget
  Player player
  Texture texture Texture/RandomScreenshot:Get

  function List CreateChildren (Widget self)
    var l List
    l +=
      Widget/Components:Margin 64 64
        Widget/Components:Expand
          Widget/Map:Create player player.GetPiloting.GetSystem
    l

  function Void PreDraw (Widget self)
    DrawPanel self.pos self.size (Vec3 0.1 0.2 0.3) 1 1 0
    Draw texture self.pos self.size 1.0

  function Void PostDraw (Widget self)
    Draw Icon/Cursors:Pointer Cursor_Get 12 4.0 * Colors:Primary 0.3
    Widget/Dev/FrameInfo:Draw self

  function Void Receive (Widget self Data data)
    switch
      (data.IsType "MessageAddContextMenu")
        ref msg (cast Widget/Messages:MessageAddContextMenu data)
        self.AddChild msg.widget

      (data.IsType "MessageAddWidget")
        ref message (cast Widget/Messages:MessageAddWidget data)
        self.AddChild
          Widget/Components:Expand
            Widget/Components:AlignCenter
              Widget/Components:Margin
                Vec2 256 128
                Vec2 256 128
                message.widget

type MapApp
  Object system
  Player player
  Interface ui
    
  function Void Initialize ()
    system = (Object_System (Vec3 15.012) 1340)

    Object/System:Init system

    for it system.GetInteriorObjects it.HasMore it.Advance
      if it.Get.GetOwner.IsNotNull
        player = it.Get.GetOwner

    ui = (Interface_Create "UI")
    ui.Add
      Layer
        Compositor_Basic 0.05 0.2 (Vec3 0.25 0.50 0.75)
        Mesh_Quad
        1.0
        Stack
          Custom Widget
            MapWidget player

  function Void Update ()
    if Key_T.Pressed Shader_RecompileAll

    var dt 10.0 * FrameTimer_Get
    system.Update dt
    ui.Update
    ui.Draw

function MapApp Main ()
  MapApp
