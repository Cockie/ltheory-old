function Int Quality () 8

function PlateMesh Main (Int seed)
  function Void AddRing (PlateMesh self Float h Float r Int spokes Float th)
    var origin (Vec3 0 h 0)
    for i 0 i < spokes i.++
      var t i / spokes
      var angle 2Pi * t
      var dir (Vec3 angle.Cos 0 angle.Sin)
      var rot (Vec3 Pi2 - angle 0 0)
    
      self.Add origin + 0.5 * r * dir (Vec3 0.2 0.1 0.5 * r) rot 0.15
      self.Add origin + (r + th) * dir (Vec3 0.5 0.5 th) rot 0.25
      self.Add origin + 0.5 * r * dir (Vec3 0.5 0.2 1) rot 0.5

  function Float ClampExp (Float variance Float t)
    Exp variance * (Max 0.0 t)

  function Void AddColumn (PlateMesh self RNG rng Float h Int pieces Float radius)
    var dh (Float_Divide h pieces)
    for i 0 i < pieces i.++
      var y i / pieces
      y = 2.0 * y - 1.5
      var r (0.1 + 0.4 * radius * rng.Exp ^ 0.5 * (ClampExp -0.5 1.0 - y))
      self.Add
        Vec3 0 h * y 0
        Vec3 r 1.5 * dh r
        0
        0.25

  var self (PlateMesh_Create Quality)
  var rng (RNG_MTG seed)
  self.AddRing -1 4.0  8 0.05
  self.AddRing  0 8.0 2 * (rng.Int 2 8) 0.1
  self.AddRing  1 3.0  4 0.05
  self.AddRing -4 2.0  6 0.05
  self.AddColumn rng 8 2 * (rng.Int 8 16) 2

  self.Add (Warp_AttractorPoint (Vec3 0 10 0) 8.0 * rng.Float)
  self.Add (Warp_AttractorPoint (Vec3 0 -10 0) 8.0 * rng.Float)

  self
